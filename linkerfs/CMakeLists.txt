if (BUILD_SHARED_LIBS)
    set(CMAKE_FIND_LIBRARY_SUFFIXES ".so" ".dll.a")
else ()
    set(CMAKE_FIND_LIBRARY_SUFFIXES ".a" ".lib")
endif ()

# find liblinkerfs
find_library(LIB_linkerfs NAMES linkerfs PATHS ${CMAKE_CURRENT_SOURCE_DIR}/binary/liblinkerfs/lib NO_CACHE)
if (LIB_linkerfs)
    message(STATUS "Found Library linkerfs: ${LIB_linkerfs}")
    add_library(liblinkerfs UNKNOWN IMPORTED GLOBAL)
    set_target_properties(liblinkerfs PROPERTIES IMPORTED_LOCATION ${LIB_linkerfs})
    target_include_directories(liblinkerfs INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/binary/liblinkerfs/include)
    file(READ ${CMAKE_CURRENT_SOURCE_DIR}/binary/liblinkerfs/include/liblinkerfs/config.h LIBLINKERFS_CONFIG_H)
    string(REGEX MATCH "#define[ \t]+LIBLINKERFS_ENABLE_UDF[ \t]+[01]" UDF_FEATURE_MATCH "${LIBLINKERFS_CONFIG_H}")
    string(REGEX REPLACE "#define[ \t]+LIBLINKERFS_ENABLE_UDF[ \t]+" "" UDF_FEATURE_VALUE "${UDF_FEATURE_MATCH}")
    if (UDF_FEATURE_MATCH STREQUAL "")
        set(LIBLINKERFS_FEATURE_UDF OFF PARENT_SCOPE)
    elseif (UDF_FEATURE_VALUE STREQUAL "1")
        set(LIBLINKERFS_FEATURE_UDF ON PARENT_SCOPE)
    else ()
        set(LIBLINKERFS_FEATURE_UDF OFF PARENT_SCOPE)
    endif ()
else ()
    if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/liblinkerfs/CMakeLists.txt)
        add_subdirectory(liblinkerfs)
        set(LIBLINKERFS_FEATURE_UDF ${LIBLINKERFS_ENABLE_UDF} PARENT_SCOPE)
    else ()
        message(FATAL_ERROR "Not Found liblinkerfs, please run `git submodule update --recursive --init`")
    endif ()
endif ()